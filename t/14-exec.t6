use Test;
use Test::When <author>;
use Docker::API;

plan 10;

my $docker = Docker::API.new;

ok $docker.image-create(fromImage => 'alpine', tag => 'latest'), 'image pull';

ok my $created = $docker.container-create(name => 'perltesting',
                                          Image => 'alpine',
                                          Cmd => ( '/bin/sleep', '1')),
                                          'create container';

my $id = $created<Id>;

ok $docker.container-start(:$id), 'container start';

ok my $exec = $docker.exec-create(:$id, :AttachStdout,
                                  Cmd => ( '/bin/echo', 'exec testing')),
    'exec create';

my $exec-id = $exec<Id>;

ok my $output = ~$docker.exec-start(id => $exec-id), 'exec start';

is $output, "exec testing\n", 'Got output';

ok $output = ~$docker.exec(:$id, :AttachStdout,
                           Cmd => ( '/bin/echo', 'more exec testing')),
    'exec';

is $output, "more exec testing\n", 'Got output';

ok $docker.container-stop(:$id), 'stop container';

ok $docker.container-remove(:$id), 'remove';

done-testing;
