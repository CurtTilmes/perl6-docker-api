use Test;
use Test::When <author>;
use Docker::API;
use Libarchive::Simple;

plan 6;

with archive-write(my $tarfile = Buf.new, format => 'paxr')
{
    .write('Dockerfile', q:to/DOCKERFILE/);
        FROM alpine:latest
        LABEL maintainer="Curt Tilmes <Curt.Tilmes@nasa.gov>"
        ENTRYPOINT ["/bin/ash"]
        DOCKERFILE
    .close;
}

isa-ok my $docker = Docker::API.new, Docker::API;

ok $docker.image-build($tarfile, :nocache,
                       t => ['docker-perl-testing:test-version']),
    'image build';

ok my $images = $docker.images(reference => 'docker-perl-testing:test-version'),
    'image list';

with $images[0]
{
    is .<RepoTags>[0], 'docker-perl-testing:test-version', 'tag correct';
    is .<Labels><maintainer>, 'Curt Tilmes <Curt.Tilmes@nasa.gov>', 'label';
}

ok $docker.image-remove(name => 'docker-perl-testing:test-version', :force),
    'remove image';

done-testing;
